buildscript {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Legacy Fabric'
            url = 'https://repo.legacyfabric.net/repository/legacyfabric/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'fabric-loom:fabric-loom.gradle.plugin:0.2.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'fabric-loom'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { 
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/' 
    }
    maven { 
        name = 'Legacy Fabric'
        url = 'https://repo.legacyfabric.net/repository/legacyfabric/' 
    }
}

dependencies {
    // Minecraft 1.8.9
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
